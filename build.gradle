buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7"
}

group = "by.alaurenau"
version = "0.0.14"

String repoName = 'bilberry'

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

compileGroovy {
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile "de.undercouch:gradle-download-task:3.1.2"
  compile "org.apache.httpcomponents:fluent-hc:4.5.2"
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
}

publishing {
  publications {
    plugin(MavenPublication) {
      from components.java
      artifact sourcesJar

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'a gradle plugin for starting and stopping ElasticSearch during integration tests')
        root.children().last() + pomConfig
      }
    }
  }
}

task install() {
  dependsOn 'publishPluginPublicationToMavenLocal'
  doFirst {
    logger.lifecycle "installing $project.name version $version"
  }
}

model {
  tasks.generatePomFileForPluginPublication {
    destination = file("$buildDir/libs/${project.name}-${project.version}.pom")
  }
}

bintray {
  user = System.env.BINTRAY_USER
  key = System.env.BINTRAY_API_KEY

  publications = ['plugin']

  publish = true

  pkg {
    repo = 'gradle-plugins'
    name = 'gradle-elastic-plugin'
    websiteUrl = "https://github.com/alaurenau/$repoName"
    issueTrackerUrl = "https://github.com/alaurenau/$repoName/issues"
    vcsUrl = "https://github.com/alaurenau/${repoName}.git"
    licenses = ['Apache-2.0']
    labels = ['gradle', 'plugin', 'elastic', 'ElasticSearch', repoName]
    version {
      name = project.version
    }
  }
}

bintrayUpload.dependsOn build
